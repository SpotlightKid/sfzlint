%import common.SIGNED_INT -> INT
%import common.SIGNED_FLOAT -> FLOAT
%import common.DIGIT
%import common.LCASE_LETTER
%import common.CNAME -> VARNAME
%import common.WS -> _WS
%import common.NEWLINE
%import common.ESCAPED_STRING

COMMENT: "//" /./* NEWLINE
       | "/*" /(.|\n|\r)/* "*/"
%ignore COMMENT

start: (_macro | header | opcode_exp | _WS)*

header: "<" HEADR ">"

HEADR : "region" | "group" | "control" | "global" | "curve" | "effect" | "master"

_macro: (define_macro | include_macro)

define_macro: "#define" " "+ define_key " "+ define_value

?define_key: "$" VARNAME
?define_value: INT | FLOAT | NOTE_NAME | _string | ATOM

include_macro: "#include" " "+ (PATH|ESCAPED_STRING)

opcode_exp: OPCODE "=" opcode_value

OPCODE: LCASE_LETTER ("_"|LCASE_LETTER|DIGIT)+

?opcode_value: INT | FLOAT | NOTE_NAME | _string | ATOM | define_key

NOTE_NAME: LCASE_LETTER ["#"|"b"] DIGIT

ATOM: LCASE_LETTER ("_"|LCASE_LETTER)*

_string: ESCAPED_STRING | PATH

PATH: /[^=\s"'][^=\r\n]*/ "." /[a-zA-Z0-9._-~]/+
